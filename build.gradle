buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
  }
}
plugins {
    id 'org.springframework.boot' version '1.5.7.RELEASE'
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'


group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
  imports {
    mavenBom 'io.spring.platform:platform-bom:Brussels-SR5'
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

configurations {
  testCompile.extendsFrom compileOnly
}

dependencies {

  // Internal API
  compile ("com.foilen:foilen-infra-api:$foilenInfraApiVersion") {
    exclude group: 'com.foilen', module: 'foilen-infra-resource-application'
    exclude group: 'com.foilen', module: 'foilen-infra-resource-unixuser'
  }
  compile "com.foilen:foilen-infra-system-core-system-common:$foilenInfraSystemVersion"

  // Resources
  compileOnly "com.foilen:foilen-infra-resource-application:0.1.2"
  compileOnly "com.foilen:foilen-infra-resource-dns:0.1.7"
  compileOnly "com.foilen:foilen-infra-resource-example:0.1.0"
  compileOnly "com.foilen:foilen-infra-resource-example-failing:0.1.0"
  compileOnly "com.foilen:foilen-infra-resource-infraconfig:0.3.10"
  compileOnly "com.foilen:foilen-infra-resource-letsencrypt:0.1.7"
  compileOnly "com.foilen:foilen-infra-resource-machine:0.1.1"
  compileOnly "com.foilen:foilen-infra-resource-mariadb:0.1.4"
  compileOnly "com.foilen:foilen-infra-resource-testing-controller:0.1.0"
  compileOnly "com.foilen:foilen-infra-resource-unixuser:0.1.6"
  compileOnly "com.foilen:foilen-infra-resource-urlredirection:0.1.8"
  compileOnly "com.foilen:foilen-infra-resource-webcertificate:0.1.2"
  compileOnly "com.foilen:foilen-infra-resource-website:0.1.9"

  // Databases
  compile "mysql:mysql-connector-java"
  compile "org.springframework.boot:spring-boot-starter-data-jpa"
  compile "org.springframework.boot:spring-boot-starter-jdbc"
  
  // Emails
  compile 'com.sun.mail:javax.mail'
  compile 'javax.activation:activation'

  // Web
  compile "com.foilen:foilen-login-api:$foilenLoginApiVersion"
  compile "org.springframework.boot:spring-boot-starter-freemarker"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.webjars:bootstrap:3.3.7-1"
  compile "org.webjars:typeaheadjs:0.11.1"

  // Spring
  compile "org.springframework:spring-context"
  compile "org.springframework.retry:spring-retry"
  compile 'org.springframework.security:spring-security-web'

  // Misc
  compile "args4j:args4j:$argsVersion"
  compile 'org.quartz-scheduler:quartz'

  // Test
  testCompile "com.foilen:foilen-infra-system-core-system-junits:$foilenInfraSystemVersion"
  testCompile "com.h2database:h2"
  testCompile 'junit:junit'
  testCompile "org.springframework:spring-test"
  testCompile "org.springframework.boot:spring-boot-starter-test"
}

apply plugin: 'application'

mainClassName = 'com.foilen.infra.ui.InfraUiApp'

startScripts {
  unixStartScriptGenerator.template = resources.text.fromFile('scripts/customUnixStartScript.txt')
}

task wrapper(type: Wrapper) { gradleVersion = '4.7' }
